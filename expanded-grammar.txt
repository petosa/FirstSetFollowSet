<tiger-program> ::= "let" <declaration-segment> "in" <stat-seq> "end"

<declaration-segment> ::= <type-declaration-list> <var-declaration-list> <funct-declaration-list>

<type-declaration-list> ::= "NULL"
<type-declaration-list> ::= <type-declaration> <type-declaration-list>

<var-declaration-list> ::= "NULL"
<var-declaration-list> ::= <var-declaration> <var-declaration-list>

<funct-declaration-list> ::= "NULL"
<funct-declaration-list> ::= <funct-declaration> <funct-declaration-list>

<type-declaration> ::= "type" "ID" "=" <type> ";"

<type> ::= <type-id>
<type> ::= "array" "[" "INTLIT" "]" "of" <type-id>
<type> ::= "ID"

<type-id> ::= "int"
<type-id> ::= "float"

<var-declaration> ::= "var" <id-list> ":" <type> <optional-init> ";"
<id-list> ::= "ID" <id-list-tail>
<id-list-tail> ::= "NULL"
<id-list-tail> ::= "," <id-list>
<optional-init> ::= "NULL"
<optional-init> ::= ":=" <constant>

<funct-declaration> ::= "function" "ID" "(" <param-list> ")" <ret-type> "begin" <stat-seq> "end" ";"
<param-list> ::= "NULL"
<param-list> ::= <param> <param-list-tail>
<param-list-tail> ::= "NULL"
<param-list-tail> ::= "," <param> <param-list-tail>
<ret-type> ::= "NULL"
<ret-type> ::= ":" <type>
<param> ::= "ID" ":" <type>

<stat-seq> ::= <stat> <stat-seq-tail>
<stat-seq-tail> ::= "NULL"
<stat-seq-tail> ::= <stat-seq>

<stat> ::= "if" <numexpr> "then" <stat-seq> "else" <stat-seq> "endif" ";"
<stat> ::= "while" <numexpr> "do" <stat-seq> "enddo" ";"
<stat> ::= "for" "ID" ":=" <numexpr> "to" <numexpr> "do" <stat-seq> "enddo" ";"
<stat> ::= "ID" <stat-tail>
<stat-tail> ::= <lvalue-tail> ":=" <stat-tail-tail>
<stat-tail> ::= "(" <expr-list> ")" ";"
<stat-tail-tail> ::= "(" <numexpr> ")" <multexprtail> <divexprtail> <addexprtail> <subexprtail> <eqexprtail> <neqexprtail> <rexprtail> <lexprtail> <geqexprtail> <leqexprtail> <andexprtail> <numexprtail> ";"
<stat-tail-tail> ::= <constant> <multexprtail> <divexprtail> <addexprtail> <subexprtail> <eqexprtail> <neqexprtail> <rexprtail> <lexprtail> <geqexprtail> <leqexprtail> <andexprtail> <numexprtail> ";"
<stat-tail-tail> ::= "ID" <stat-tail-tail-tail>
<stat-tail-tail-tail> ::= <lvalue-tail> <multexprtail> <divexprtail> <addexprtail> <subexprtail> <eqexprtail> <neqexprtail> <rexprtail> <lexprtail> <geqexprtail> <leqexprtail> <andexprtail> <numexprtail> ";"
<stat-tail-tail-tail> ::= "(" <expr-list> ")" ";"
<optprefix> ::= <lvalue> ":="
<optprefix> ::= "NULL"
<stat> ::= "break" ";"
<stat> ::= "return" <numexpr> ";"

<stat> ::= "let" <declaration-segment> "in" <stat-seq> "end"

<numexpr> ::= <andexpr> <numexprtail>
<numexprtail> ::= "|" <andexpr> <numexprtail>
<numexprtail> ::= "NULL"
<andexpr> ::= <leqexpr> <andexprtail>
<andexprtail> ::= "&" <leqexpr> <andexprtail>
<andexprtail> ::= "NULL"
<leqexpr> ::= <geqexpr> <leqexprtail>
<leqexprtail> ::= "<=" <subexpr>
<leqexprtail> ::= "NULL"
<geqexpr> ::= <lexpr> <geqexprtail>
<geqexprtail> ::= ">=" <subexpr>
<geqexprtail> ::= "NULL"
<lexpr> ::= <rexpr> <lexprtail>
<lexprtail> ::= "<" <subexpr>
<lexprtail> ::= "NULL"
<rexpr> ::= <neqexpr> <rexprtail>
<rexprtail> ::= ">" <subexpr>
<rexprtail> ::= "NULL"
<neqexpr> ::= <eqexpr> <neqexprtail>
<neqexprtail> ::= "<>" <subexpr>
<neqexprtail> ::= "NULL"
<eqexpr> ::= <subexpr> <eqexprtail>
<eqexprtail> ::= "=" <subexpr>
<eqexprtail> ::= "NULL"
<subexpr> ::= <addexpr> <subexprtail>
<subexprtail> ::= "-" <addexpr> <subexprtail>
<subexprtail> ::= "NULL"
<addexpr> ::= <divexpr> <addexprtail>
<addexprtail> ::= "+" <divexpr> <addexprtail>
<addexprtail> ::= "NULL"
<divexpr> ::= <multexpr> <divexprtail>
<divexprtail> ::= "/" <multexpr> <divexprtail>
<divexprtail> ::= "NULL"
<multexpr> ::= <parenexpr> <multexprtail>
<multexprtail> ::= "*" <parenexpr> <multexprtail>
<multexprtail> ::= "NULL"
<parenexpr> ::= "(" <numexpr> ")"
<parenexpr> ::= <term>

<term> ::= <constant>
<term> ::= <lvalue>
<constant> ::= "INTLIT"
<constant> ::= "FLOATLIT"

<expr-list> ::= "NULL"
<expr-list> ::= <numexpr> <expr-list-tail>
<expr-list-tail> ::= "," <numexpr> <expr-list-tail>
<expr-list-tail> ::= "NULL"

<lvalue> ::= "ID" <lvalue-tail>
<lvalue-tail> ::= "[" <numexpr> "]"
<lvalue-tail> ::= "NULL"